// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t]==1) out[t+1] = 0
 * else if (load[t]==1)  out[t+1] = in[t]
 * else if (inc[t]==1)   out[t+1] = out[t] + 1  (integer addition)
 * else                  out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // priority: reset load inc

    // inc
    Add16(a=regOut, b[0]=true, out=addOut);
    Mux16(a=regOut, b=addOut, sel=inc, out=incrementOut);

    // load
    Mux16(a=incrementOut, b=in, sel=load, out=loadOut);

    // reset
    Mux16(a=loadOut, b=false, sel=reset, out=resetOut);

    Register(in=resetOut, load=true, out=out, out=regOut);
}




